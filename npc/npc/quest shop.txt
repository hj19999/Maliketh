//===== rAthena Script =======================================
//= Euphy's Quest Shop
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.6c
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= A dynamic quest shop based on Lunar's, with easier config.
//= Includes support for multiple shops & cashpoints.
//= Item Preview script by ToastOfDoom.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.2 Added category support.
//= 1.3 More options and fixes.
//= 1.4 Added debug settings.
//= 1.5 Replaced categories with shop IDs.
//= 1.6 Added support for purchasing stackables.
//= 1.6a Added support for previewing costumes and robes.
//= 1.6b Added 'disable_items' command.
//= 1.6c Replaced function 'A_An' with "F_InsertArticle".
//============================================================

// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "qshop"{,<shop ID>{,<shop ID>{,...}}};
//============================================================
doubrius,35,41,6	script	Quest Shop#1	997,{ callfunc "qshop",1; }
doubrius,35,29,6	script	Event Shop#1	459,{ callfunc "qshop",2; }
doubrius,31,41,6	script	Ramadan Pass Shop#1	526,{ callfunc "qshop",3; }
doubrius,35,33,6	script	MVP Shop#1	796,{ callfunc "qshop",4; }
doubrius,35,25,6	script	WoE Shop#1	413,{ callfunc "qshop",5; }


// Script Core
//============================================================
-	script	quest_shop	-1,{ 
function Add; function Chk; function Slot;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .Announce,1;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,0;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,0;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,100;	// Max number of quest items purchased at one time.

// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],"#CASHPOINTS","Cash Points";

// -----------------------------------------------------------
//  Shop IDs -- to add shops, copy dummy data at bottom of file.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	setarray .Shops$[1],"Quest Shop","Event Shop","Ramadan Pass Shop","MVP Shop","WoE Shop";

// -----------------------------------------------------------
//  Quest items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//	    <Zeny cost>,<point cost>,
//	    <required item ID>,<required item amount>{,...});
// -----------------------------------------------------------



	Add(1,7619,1,12000000,0,56002,140);
	Add(1,7620,1,12000000,0,56002,140);
	Add(1,5208,1,89000000,0,56002,1400);
	Add(1,5374,1,209000000,0,56002,3200);
	Add(1,5518,1,209000000,0,56002,3200);	
	Add(1,18561,1,189000000,0,56002,2800);
	Add(1,2168,1,179000000,0,56002,2600);
	Add(1,5376,1,134000000,0,56002,2000);
	Add(1,18539,1,134000000,0,56002,2000);
	Add(1,5359,1,119000000,0,56002,1800);
	Add(1,5481,1,119000000,0,56002,1800);
	Add(1,5388,1,104000000,0,56002,1600);
	

	Add(2,6417,1,0,0,56000,135);
	Add(2,17315,1,0,0,56000,1200);
	Add(2,12211,1,0,0,56000,150);
	Add(2,56005,1,0,0,56000,3500);
	Add(2,7620,1,0,0,56000,250);
	Add(2,7619,1,0,0,56000,250);
	Add(2,6240,1,0,0,56000,300);
	Add(2,6241,1,0,0,56000,300);
	Add(2,12210,1,0,0,56000,500);
	Add(2,7776,1,0,0,56000,325);
	Add(2,14533,1,0,0,56000,225);	
	Add(2,7621,1,0,0,56000,175);
	Add(2,12221,1,0,0,56000,150);
	Add(2,12214,1,0,0,56000,100);
	Add(2,12212,1,0,0,56000,100);	



	Add(3,18666,1,0,0,56004,3500);
	Add(3,19903,1,0,0,56004,8000);
	Add(3,18813,1,0,0,56004,5500);
	Add(3,19827,1,0,0,56004,8000);
	Add(3,12211,1,0,0,56004,275);
	Add(3,60007,1,0,0,56004,8000);
	Add(3,60003,1,0,0,56004,8000);
	Add(3,60004,1,0,0,56004,8000);
	Add(3,60005,1,0,0,56004,8000);
	Add(3,60013,1,0,0,56004,8000);
	Add(3,60023,1,0,0,56004,8000);
	Add(3,60031,1,0,0,56004,8000);
	Add(3,60037,1,0,0,56004,8000);
	Add(3,60038,1,0,0,56004,8000);
	Add(3,5325,1,0,0,56004,4500);
	Add(3,5597,1,0,0,56004,3500);
	Add(3,7621,1,0,0,56004,275);
	Add(3,12221,1,0,0,56004,275);
	Add(3,12208,1,0,0,56004,175);
	Add(3,12202,1,0,0,56004,80);
	Add(3,12203,1,0,0,56004,80);
	Add(3,12204,1,0,0,56004,80);
	Add(3,12205,1,0,0,56004,80);
	Add(3,12206,1,0,0,56004,80);
	Add(3,12207,1,0,0,56004,80);	






	Add(4,17251,1,0,0,56001,45);
	Add(4,5155,1,0,0,56001,250);
	Add(4,5377,1,0,0,56001,250);
	Add(4,5490,1,0,0,56001,200);
	Add(4,5463,1,0,0,56001,190);
	Add(4,5462,1,0,0,56001,190);
	Add(4,12412,1,0,0,56001,45);
	Add(4,6241,1,0,0,56001,20);
	Add(4,6240,1,0,0,56001,20);
	Add(4,12214,1,0,0,56001,4);
	Add(4,12212,1,0,0,56001,2);	

	
	Add(5,12913,1,0,0,50002,8);
	Add(5,12914,1,0,0,50002,8);
	Add(5,12679,1,0,0,50002,1);
	Add(5,12680,1,0,0,50002,1);	
	Add(5,18509,1,0,0,50002,85);	
	Add(5,20510,1,0,0,50002,50);
	Add(5,12412,1,0,0,50002,12);
	Add(5,12210,1,0,0,50002,5);	
	Add(5,7619,1,0,0,50002,4);	
	Add(5,7620,1,0,0,50002,4);	
	Add(5,6241,1,0,0,50002,6);	
	Add(5,6240,1,0,0,50002,6);	
	Add(5,6225,1,0,0,50002,6);	
	Add(5,6226,1,0,0,50002,6);	
	Add(5,17315,1,0,0,50002,22);	
	Add(5,12903,1,0,0,50002,10);	
	Add(5,12904,1,0,0,50002,10);	
	Add(5,12905,1,0,0,50002,10);	
	Add(5,12906,1,0,0,50002,10);	
	Add(5,12907,1,0,0,50002,10);	
	Add(5,12908,1,0,0,50002,10);	


	


// -----------------------------------------------------------

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "qshop"+.@i,909;
	}
	end;

OnMenu:
	set .@size, getarraysize(@i);
	if (!.@size) set @shop_index, select(.menu$);
	else if (.@size == 1) set @shop_index, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set @shop_index, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[@shop_index] == "") {
		message strcharinfo(0),"An error has occurred.";
		end;
	}
	dispbottom "Select one item at a time.";
	callshop "qshop"+@shop_index,1;
	npcshopattach "qshop"+@shop_index;
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+@shop_index+"_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+@shop_index+"_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	mes "[Quest Shop]";
	mes "Reward: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requirements:";
	disable_items;
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0],5), getiteminfo(.@q[0],11);
	if (@qe[2] > 0 && ((@qe[1] & 1) || (@qe[1] & 256) || (@qe[1] & 512) || (@qe[1] & 1024) || (@qe[1] & 2048) || (@qe[1] & 4096) || (@qe[1] & 4) || (@qe[1] & 8192)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ Purchase ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?" ~ Preview...":"")+": ~ ^777777Cancel^000000")) {
		case 1:
			if (@qe[0]) { 
				mes "[Quest Shop]";
				mes "You're missing one or more quest requirements.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Quest Shop]";
				mes "^FF0000You need "+(((.@q[2]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" additional weight capacity to complete this trade.^000000";
				close;
			}
			if (.@q[4]) set Zeny, Zeny-(.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if (.Announce) announce strcharinfo(0)+" has created "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):callfunc("F_InsertArticle",getitemname(.@q[0])))+"!",0;
			specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {
	if (getitemname(getarg(1)) == "null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(0)+"_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "qshop"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),2) == 4 || getiteminfo(getarg(0),2) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}
}

function	script	qshop	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "quest_shop::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	qshop1	-1,909:-1
-	shop	qshop2	-1,909:-1
-	shop	qshop3	-1,909:-1
-	shop	qshop4	-1,909:-1
-	shop	qshop5	-1,909:-1
-	shop	qshop6	-1,909:-1