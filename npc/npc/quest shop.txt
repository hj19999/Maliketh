//===== rAthena Script =======================================
//= Euphy's Quest Shop
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.6c
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= A dynamic quest shop based on Lunar's, with easier config.
//= Includes support for multiple shops & cashpoints.
//= Item Preview script by ToastOfDoom.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.2 Added category support.
//= 1.3 More options and fixes.
//= 1.4 Added debug settings.
//= 1.5 Replaced categories with shop IDs.
//= 1.6 Added support for purchasing stackables.
//= 1.6a Added support for previewing costumes and robes.
//= 1.6b Added 'disable_items' command.
//= 1.6c Replaced function 'A_An' with "F_InsertArticle".
//============================================================

// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "qshop"{,<shop ID>{,<shop ID>{,...}}};
//============================================================
doubrius,35,41,4	script	Quest Shop#1	900,{ callfunc "qshop",1,2,3; }
doubrius,35,35,4	script	MVP Shop#1	796,{ callfunc "qshop",4; }


// Script Core
//============================================================
-	script	quest_shop	-1,{ 
function Add; function Chk; function Slot;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .Announce,1;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,0;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,0;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,100;	// Max number of quest items purchased at one time.

// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],"#CASHPOINTS","Cash Points";

// -----------------------------------------------------------
//  Shop IDs -- to add shops, copy dummy data at bottom of file.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	setarray .Shops$[1],"Quest Shop","Poring Shop","Event Shop","MVP Shop";

// -----------------------------------------------------------
//  Quest items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//	    <Zeny cost>,<point cost>,
//	    <required item ID>,<required item amount>{,...});
// -----------------------------------------------------------


	Add(1,32263,1,100000000,0,56002,1750);
	Add(1,28521,1,150000000,0,56002,2000);
	Add(1,18540,1,75000000,0,56002,1500);
	Add(1,18563,1,75000000,0,56002,1500);
	Add(1,19439,1,100000000,0,56002,1750);
	Add(1,32242,1,150000000,0,56002,4000);
	Add(1,22172,1,150000000,0,56002,3500);
	Add(1,2576,1,100000000,0,56002,2000);
	Add(1,19125,1,150000000,0,56002,2000);
	Add(1,5933,1,75000000,0,56002,1500);
	Add(1,19112,1,100000000,0,56002,1500);



	Add(2,18794,1,150,0,56002,1500);
	Add(2,28902,1,150,0,56002,4000);
	Add(2,5900,1,150,0,56002,1500);
	Add(2,2168,1,150,0,56002,1900);
	Add(2,5592,1,150,0,56002,1500);
	Add(2,18813,1,150,0,56002,1500);
	Add(2,5286,1,150,0,56002,1200);
	Add(2,19116,1,150,0,56002,1500);
	Add(2,5901,1,150,0,56002,1500);
	Add(2,5902,1,150,0,56002,1500);
	Add(2,5903,1,150,0,56002,1500);
	Add(2,5904,1,150,0,56002,1500);
	Add(2,5905,1,150,0,56002,1500);
	Add(2,5906,1,150,0,56002,1500);
	Add(2,5907,1,150,0,56002,1500);
	Add(2,18793,1,150,0,56002,1500);
	Add(2,5800,1,150,0,56002,2200);
	Add(2,18561,1,150,0,56002,1700);
	Add(2,5376,1,150,0,56002,1700);
	Add(2,2589,1,150,0,56002,2500);
	Add(2,12208,1,150,0,56002,100);
	Add(2,12210,1,300,0,56002,120);
	Add(2,5208,1,1200,0,56002,800);
	Add(2,5490,1,1000,0,56002,1000);
	Add(2,5481,1,1000,0,56002,1100);
	Add(2,5388,1,1000,0,56002,700);
	Add(2,5597,1,1000,0,56002,1000);
	Add(2,5403,1,1000,0,56002,1000);
	Add(2,5359,1,1200,0,56002,1200);
	Add(2,5463,1,1200,0,56002,1200);
	Add(2,5462,1,1200,0,56002,1200);
	Add(2,18539,1,1500,0,56002,1500);
	Add(2,5361,1,1500,0,56002,1500);
	Add(2,5389,1,1500,0,56002,1500);
	Add(2,5325,1,1500,0,56002,1500);
	Add(2,5377,1,1500,0,56002,1500);




	Add(3,31389,1,0,0,56000,100);
	Add(3,20302,1,0,0,56000,100);
	Add(3,19960,1,0,0,56000,100);
	Add(3,19520,1,0,0,56000,100);
	Add(3,6225,1,0,0,56000,55);
	Add(3,6226,1,0,0,56000,55);
	Add(3,17315,1,0,0,56000,100);
	Add(3,6417,1,0,0,56000,30);
	Add(3,14159,1,500000,0,56000,5);
	Add(3,14160,1,500000,0,56000,5);
	Add(3,14161,1,500000,0,56000,5);
	Add(3,14162,1,500000,0,56000,5);
	Add(3,14163,1,500000,0,56000,5);
	Add(3,14164,1,500000,0,56000,5);
	Add(3,7776,1,0,0,56000,15);
	Add(3,12214,1,0,0,56000,50);
	Add(3,12221,1,0,0,56000,30);
	Add(3,6241,1,0,0,56000,50);
	Add(3,6240,1,0,0,56000,50);
	Add(3,7619,1,0,0,56000,35);
	Add(3,7620,1,0,0,56000,35);
	Add(3,13534,1,0,0,56000,10);
	Add(3,13810,1,0,0,56000,10);
	Add(3,13698,1,0,0,56000,10);
	Add(3,13697,1,0,0,56000,10);
	Add(3,14171,1,0,0,56000,10);












	Add(4,6241,1,0,0,56001,20);
	Add(4,6240,1,0,0,56001,20);
	Add(4,12210,1,0,0,56001,10);
	Add(4,20458,1,0,0,56001,75);
	Add(4,20086,1,0,0,56001,75);
	Add(4,31387,1,0,0,56001,75);
	Add(4,31396,1,0,0,56001,75);
	Add(4,31437,1,0,0,56001,75);
	Add(4,20325,1,0,0,56001,75);
	Add(4,31120,1,0,0,56001,75);
	Add(4,20315,1,0,0,56001,75);
	Add(4,20132,1,0,0,56001,75);
	Add(4,20285,1,0,0,56001,75);
	Add(4,20764,1,0,0,56001,75);
	Add(4,20514,1,0,0,56001,75);
	Add(4,20509,1,0,0,56001,75);
	


// -----------------------------------------------------------

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "qshop"+.@i,909;
	}
	end;

OnMenu:
	set .@size, getarraysize(@i);
	if (!.@size) set @shop_index, select(.menu$);
	else if (.@size == 1) set @shop_index, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set @shop_index, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[@shop_index] == "") {
		message strcharinfo(0),"An error has occurred.";
		end;
	}
	dispbottom "Select one item at a time.";
	callshop "qshop"+@shop_index,1;
	npcshopattach "qshop"+@shop_index;
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+@shop_index+"_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+@shop_index+"_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	mes "[Quest Shop]";
	mes "Reward: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requirements:";
	disable_items;
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0],5), getiteminfo(.@q[0],11);
	if (@qe[2] > 0 && ((@qe[1] & 1) || (@qe[1] & 256) || (@qe[1] & 512) || (@qe[1] & 1024) || (@qe[1] & 2048) || (@qe[1] & 4096) || (@qe[1] & 4) || (@qe[1] & 8192)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ Purchase ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?" ~ Preview...":"")+": ~ ^777777Cancel^000000")) {
		case 1:
			if (@qe[0]) { 
				mes "[Quest Shop]";
				mes "You're missing one or more quest requirements.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Quest Shop]";
				mes "^FF0000You need "+(((.@q[2]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" additional weight capacity to complete this trade.^000000";
				close;
			}
			if (.@q[4]) set Zeny, Zeny-(.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if (.Announce) announce strcharinfo(0)+" has created "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):callfunc("F_InsertArticle",getitemname(.@q[0])))+"!",0;
			specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {
	if (getitemname(getarg(1)) == "null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(0)+"_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "qshop"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),2) == 4 || getiteminfo(getarg(0),2) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}
}

function	script	qshop	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "quest_shop::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	qshop1	-1,909:-1
-	shop	qshop2	-1,909:-1
-	shop	qshop3	-1,909:-1
-	shop	qshop4	-1,909:-1
-	shop	qshop5	-1,909:-1
-	shop	qshop6	-1,909:-1