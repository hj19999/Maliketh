//===== rAthena Script =======================================
//= Euphy's Quest Shop
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.6
//===== Compatible With: =====================================
//= rAthena SVN r16862+
//===== Description: =========================================
//= A dynamic quest shop based on Lunar's, with easier config.
//= Includes support for multiple shops & cashpoints.
//= Item Preview script by ToastOfDoom.
//============================================================

// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "qshop"{,<shop ID>{,<shop ID>{,...}}};
//============================================================
skycity,187,174,4	script	Quest Shop#1	874,{ callfunc "qshop"; }


// Script Core
//============================================================
-	script	quest_shop	-1,{ 
function Add; function Chk; function Slot; function A_An;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .Announce,1;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,0;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,0;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,100;	// Max number of quest items purchased at one time.

// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],"#CASHPOINTS","Cash Points";

// -----------------------------------------------------------
//  Shop IDs -- to add shops, copy dummy data at bottom of file.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	setarray .Shops$[1],"Poring Coin shop","Event Ticket shop","MVP Ticket shop","Halloween Event";

// -----------------------------------------------------------
//  Quest items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//	    <Zeny cost>,<point cost>,
//	    <required item ID>,<required item amount>{,...});
// -----------------------------------------------------------
	

	Add(1,5800,1,150,0,50003,2200);
	Add(1,18561,1,150,0,50003,1700);
	Add(1,5376,1,150,0,50003,1700);
	Add(1,2589,1,150,0,50003,2500);
	Add(1,12208,1,150,0,50003,100);
	Add(1,12210,1,300,0,50003,120);
	Add(1,5208,1,1200,0,50003,800);
	Add(1,5490,1,1000,0,50003,1000);
	Add(1,5481,1,1000,0,50003,1100);
	Add(1,5388,1,1000,0,50003,700);
	Add(1,5597,1,1000,0,50003,1000);
	Add(1,5403,1,1000,0,50003,1000);
	Add(1,5359,1,1200,0,50003,1200);
	Add(1,5463,1,1200,0,50003,1200);
	Add(1,5462,1,1200,0,50003,1200);
	Add(1,18539,1,1500,0,50003,1500);
	Add(1,5361,1,1500,0,50003,1500);
	Add(1,5389,1,1500,0,50003,1500);
	Add(1,5325,1,1500,0,50003,1500);
	Add(1,5377,1,1500,0,50003,1500);




	Add(2,31389,1,0,0,50005,100);
	Add(2,20302,1,0,0,50005,100);
	Add(2,19960,1,0,0,50005,100);
	Add(2,19520,1,0,0,50005,100);
	Add(2,14159,1,500000,0,50005,5);
	Add(2,14160,1,500000,0,50005,5);
	Add(2,14161,1,500000,0,50005,5);
	Add(2,14162,1,500000,0,50005,5);
	Add(2,14163,1,500000,0,50005,5);
	Add(2,14164,1,500000,0,50005,5);
	Add(2,7776,1,0,0,50005,15);
	Add(2,12214,1,0,0,50005,50);
	Add(2,12221,1,0,0,50005,30);
	Add(2,6241,1,0,0,50005,50);
	Add(2,6240,1,0,0,50005,50);
	Add(2,7619,1,0,0,50005,35);
	Add(2,7620,1,0,0,50005,35);
	Add(2,13534,1,0,0,50005,10);
	Add(2,13810,1,0,0,50005,10);
	Add(2,13698,1,0,0,50005,10);
	Add(2,13697,1,0,0,50005,10);
	Add(2,14171,1,0,0,50005,10);














	Add(3,20458,1,0,0,50004,75);
	Add(3,20086,1,0,0,50004,75);
	Add(3,31387,1,0,0,50004,75);
	Add(3,31396,1,0,0,50004,75);
	Add(3,31437,1,0,0,50004,75);
	Add(3,20325,1,0,0,50004,75);
	Add(3,31120,1,0,0,50004,75);
	Add(3,20315,1,0,0,50004,75);
	Add(3,20132,1,0,0,50004,75);
	Add(3,20285,1,0,0,50004,75);
	Add(3,20764,1,0,0,50004,75);
	Add(3,20514,1,0,0,50004,75);
	Add(3,20509,1,0,0,50004,75);








	Add(4,20130,1,0,0,50008,500,50009,100,50010,25);
	Add(4,19542,1,0,0,50008,500,50009,100,50010,25);
	Add(4,19903,1,0,0,50008,500,50009,100,50010,25);
	Add(4,19910,1,0,0,50008,500,50009,100,50010,25);
	Add(4,19920,1,0,0,50008,500,50009,100,50010,25);
	Add(4,19917,1,0,0,50008,500,50009,100,50010,25);
	Add(4,12208,1,0,0,50008,100);
	Add(4,12214,1,0,0,50008,100);




 








// -----------------------------------------------------------

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "qshop"+.@i,909;
	}
	end;

OnMenu:
	set .@size, getarraysize(@i);
	if (!.@size) set .@i, select(.menu$);
	else if (.@size == 1) set .@i, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set .@i, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[.@i] == "") {
		message strcharinfo(0),"An error has occurred.";
		end;
	}
	dispbottom "Select one item at a time.";
	callshop "qshop"+.@i,1;
	npcshopattach "qshop"+.@i;
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	mes "[Quest Shop]";
	mes "Reward: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requirements:";
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0],5), getiteminfo(.@q[0],11);
	if (((@qe[1] & 1) || (@qe[1] & 256) || (@qe[1] & 512)) && @qe[2] > 0)
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ Purchase ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[6])?" ~ Preview...":"")+": ~ ^777777Cancel^000000")) {
		case 1:
			if (@qe[0]) { 
				mes "[Quest Shop]";
				mes "You're missing one or more quest requirements.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Quest Shop]";
				mes "^FF0000You need "+(((.@q[2]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" additional weight capacity to complete this trade.^000000";
				close;
			}
			if (.@q[4]) set Zeny, Zeny-(.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if (.Announce) announce strcharinfo(0)+" has created "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):A_An(getitemname(.@q[0])))+"!",0;
			specialeffect2 699;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), 1;
			if (@qe[1] & 1) changelook LOOK_HEAD_BOTTOM, @qe[2];
			if (@qe[1] & 256) changelook LOOK_HEAD_TOP, @qe[2];
			if (@qe[1] & 512) changelook LOOK_HEAD_MID, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[6]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
	}
	deletearray @qe[0],7;
	end;

function Add {
	if (getitemname(getarg(1)) == "null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "qshop"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),2) == 4 || getiteminfo(getarg(0),2) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}

function A_An {
	setarray .@A$[0],"a","e","i","o","u";
	set .@B$, "_"+getarg(0);
	for(set .@i,0; .@i<5; set .@i,.@i+1)
		if (compare(.@B$,"_"+.@A$[.@i])) return "an "+getarg(0);
	return "a "+getarg(0);
}
}

function	script	qshop	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "quest_shop::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	qshop1	-1,909:-1
-	shop	qshop2	-1,909:-1
-	shop	qshop3	-1,909:-1
-	shop	qshop4	-1,909:-1
-	shop	qshop5	-1,909:-1